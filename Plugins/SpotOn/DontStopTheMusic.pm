package Plugins::SpotOn::DontStopTheMusic;

use strict;
use warnings;

use Digest::MD5 qw(md5_hex);
use Encode qw(encode_utf8);
use List::Util qw(min);
use Time::HiRes;

use Slim::Plugin::DontStopTheMusic::Plugin;
use Slim::Schema;
use Slim::Utils::Cache;
use Slim::Utils::Log;
use Slim::Utils::Prefs;
use Slim::Utils::Timers;

my $log   = Slim::Utils::Log->logger('plugin.spoton');
my $prefs = Slim::Utils::Prefs::preferences('plugin.spoton');
my $cache = Slim::Utils::Cache->new('spoton', Plugins::SpotOn::Plugin::SPOTON_CACHE_VERSION());

use constant DSTM_MAX_TRACKS       => 10;
use constant DSTM_POOL_INJECT      => 3;
use constant DSTM_SEARCH_STAGGER_S => 0.2;
use constant DSTM_POOL_TTL         => 1800;
use constant DSTM_RECENT_TTL       => 86400;
use constant DSTM_RECENT_MAX       => 30;
use constant DSTM_TAG_TTL          => 604800;

sub init {
    Slim::Plugin::DontStopTheMusic::Plugin->registerHandler(
        'PLUGIN_SPOTON_RECOMMENDATIONS',
        \&dontStopTheMusic
    );
}

sub _dstmTagKey {
    my ($artist, $title) = @_;
    return 'spoton_dstm_tag_' . md5_hex(encode_utf8(lc($artist // '') . '|' . lc($title // '')));
}

sub dontStopTheMusic {
    my ($client, $cb) = @_;

    my $seedTracks = Slim::Plugin::DontStopTheMusic::Plugin->getMixableProperties($client, 20);

    if (!$seedTracks || !ref $seedTracks || !scalar @$seedTracks) {
        $cb->($client);
        return;
    }

    my $accountId = $prefs->client($client)->get('activeAccount')
                 || $prefs->get('activeAccount')
                 || '';
    unless ($accountId) {
        $cb->($client);
        return;
    }

    require Plugins::SpotOn::API::Client;

    my (@seedArtists, %seen);
    foreach my $track (@$seedTracks) {
        next unless $track->{artist};
        next if $cache->get(_dstmTagKey($track->{artist}, $track->{title}));
        my ($first) = split /,\s*/, $track->{artist};
        next unless $first && !$seen{lc $first}++;
        push @seedArtists, $first;
    }

    _shuffle(\@seedArtists);
    splice @seedArtists, 3 if @seedArtists > 3;

    _withDiversityPool($accountId, sub {
        my ($pool) = @_;

        if (@seedArtists) {
            main::INFOLOG && $log->info("SpotOn DSTM: mixing from artists: " . join(', ', @seedArtists));
            _searchArtists($client, $accountId, \@seedArtists, 0, [], $pool, $cb);
        }
        else {
            main::INFOLOG && $log->info("SpotOn DSTM: no organic seeds, using pool only");
            _finalizeResults($client, [], $pool, $cb);
        }
    });
}

sub _withDiversityPool {
    my ($accountId, $cb) = @_;
    my $key = 'spoton_dstm_pool_' . $accountId;
    my $pool = $cache->get($key);
    if ($pool && ref $pool eq 'ARRAY' && @$pool) {
        return $cb->($pool);
    }
    Plugins::SpotOn::API::Client->getTopTracks($accountId, {
        time_range => 'medium_term',
        limit      => 50,
    }, sub {
        my $result = shift;
        $pool = ($result && $result->{items}) ? $result->{items} : [];
        $cache->set($key, $pool, DSTM_POOL_TTL) if @$pool;
        $cb->($pool);
    });
}

sub _searchArtists {
    my ($client, $accountId, $artists, $idx, $allTracks, $pool, $cb) = @_;

    if ($idx >= scalar @$artists) {
        _finalizeResults($client, $allTracks, $pool, $cb);
        return;
    }

    my $artist = $artists->[$idx];
    my $limit  = Plugins::SpotOn::API::Client->getLimit('search') || 10;
    my $perArtist = int(DSTM_MAX_TRACKS / scalar(@$artists)) + 1;
    $limit = min($limit, $perArtist);
    my $offset = int(rand(3)) * $perArtist;

    Plugins::SpotOn::API::Client->search($accountId, {
        q      => sprintf('artist:"%s"', $artist),
        type   => 'track',
        limit  => $limit,
        offset => $offset,
    }, sub {
        my $result = shift;
        my $tracks = ($result && $result->{tracks} && $result->{tracks}{items})
            ? $result->{tracks}{items} : [];

        if (!@$tracks && $offset > 0) {
            Plugins::SpotOn::API::Client->search($accountId, {
                q      => sprintf('artist:"%s"', $artist),
                type   => 'track',
                limit  => $limit,
                offset => 0,
            }, sub {
                my $result2 = shift;
                my $tracks2 = ($result2 && $result2->{tracks} && $result2->{tracks}{items})
                    ? $result2->{tracks}{items} : [];
                push @$allTracks, @$tracks2;
                Slim::Utils::Timers::setTimer(undef, Time::HiRes::time() + DSTM_SEARCH_STAGGER_S,
                    sub { _searchArtists($client, $accountId, $artists, $idx + 1, $allTracks, $pool, $cb) });
            });
            return;
        }

        push @$allTracks, @$tracks;

        Slim::Utils::Timers::setTimer(undef, Time::HiRes::time() + DSTM_SEARCH_STAGGER_S,
            sub { _searchArtists($client, $accountId, $artists, $idx + 1, $allTracks, $pool, $cb) });
    });
}

sub _finalizeResults {
    my ($client, $allTracks, $pool, $cb) = @_;

    my $clientId = $client->id();
    my $recentKey = 'spoton_dstm_recent_' . $clientId;
    my $recentUris = $cache->get($recentKey) || [];
    my %recentSet = map { $_ => 1 } @$recentUris;

    my @filtered;
    for my $t (@$allTracks) {
        next unless $t->{uri} && $t->{uri} =~ /(track:[a-z0-9]+)/i;
        my $uri = "spoton://$1";
        push @filtered, $t unless $recentSet{$uri};
    }

    if ($pool && @$pool) {
        my %searchArtists;
        for my $t (@filtered) {
            for my $a (@{$t->{artists} || []}) {
                $searchArtists{lc($a->{name})}++ if $a->{name};
            }
        }

        my $injected = 0;
        my @poolShuffled = @$pool;
        _shuffle(\@poolShuffled);
        for my $t (@poolShuffled) {
            last if $injected >= DSTM_POOL_INJECT;
            next unless $t->{uri} && $t->{uri} =~ /(track:[a-z0-9]+)/i;
            my $uri = "spoton://$1";
            next if $recentSet{$uri};
            my $primaryArtist = ($t->{artists} && @{$t->{artists}}) ? lc($t->{artists}[0]{name} // '') : '';
            next if $primaryArtist && $searchArtists{$primaryArtist};
            push @filtered, $t;
            $injected++;
        }
    }

    unless (@filtered) {
        $cb->($client);
        return;
    }

    _shuffle(\@filtered);
    splice @filtered, DSTM_MAX_TRACKS if @filtered > DSTM_MAX_TRACKS;

    my @uris = _cacheAndExtractUris(\@filtered);

    push @$recentUris, @uris;
    splice @$recentUris, 0, (@$recentUris - DSTM_RECENT_MAX) if @$recentUris > DSTM_RECENT_MAX;
    $cache->set($recentKey, $recentUris, DSTM_RECENT_TTL);

    if (@uris) {
        main::INFOLOG && $log->info("SpotOn DSTM: queuing " . scalar(@uris) . " tracks");
        $cb->($client, \@uris);
    }
    else {
        $cb->($client);
    }
}

sub _cacheAndExtractUris {
    my ($tracks) = @_;
    my @uris;

    require Plugins::SpotOn::Plugin;
    my $type_str = Plugins::SpotOn::Plugin->_typeString(undef, 'Browse');

    for my $track (@$tracks) {
        next unless $track->{uri} && $track->{uri} =~ /(track:[a-z0-9]+)/i;
        my $uri = "spoton://$1";

        my $artist = join(', ', map { $_->{name} } @{ $track->{artists} || [] });
        my $images = $track->{album}{images} || [];
        my $image  = @$images ? (sort { ($b->{width}||0) <=> ($a->{width}||0) } @$images)[0]->{url} : '';
        my $year   = Plugins::SpotOn::Plugin::_releaseYear(($track->{album} || {})->{release_date});

        my %trackIds = Plugins::SpotOn::Plugin::_extractTrackIds($track);
        $cache->set('spoton_meta_' . md5_hex($uri), {
            title    => $track->{name} // '',
            artist   => $artist,
            album    => $track->{album}{name} // '',
            duration => ($track->{duration_ms} || 0) / 1000,
            cover    => $image,
            icon     => $image,
            year     => $year,
            bitrate  => Plugins::SpotOn::Plugin->_bitrateForClient(undef) . 'k',
            type     => $type_str,
            %trackIds,
        }, 604800);

        $cache->set(_dstmTagKey($artist, $track->{name}), 1, DSTM_TAG_TTL);

        push @uris, $uri;
    }

    return @uris;
}

sub _shuffle {
    my ($arr) = @_;
    for my $i (reverse 1 .. $#$arr) {
        my $j = int(rand($i + 1));
        @$arr[$i, $j] = @$arr[$j, $i];
    }
}

1;
